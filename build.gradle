// Apply to All Projects
allprojects {

    // Plug-ins for all projects
    apply plugin: 'java'
    apply plugin: 'java-library'

    // In this section you declare where to find the dependencies of all projects
    repositories {
        jcenter()
        mavenCentral()
    }

    // Common Dependencies to all Projects
    dependencies {

        // Use JUnit test framework
        testImplementation 'junit:junit:4.12'
    }
}

// Plug-ins for main project
apply plugin: 'eclipse'
apply plugin: 'idea'

// Dependencies for main project
dependencies {
    implementation project(":core")
    implementation project(":service")
    implementation project(":cli")
    implementation project(":client")
    implementation project(":inspector")
}



task runNode(type: JavaExec, dependsOn:classes) {
    group = "run"
    description = "Starts a new TucsonNode with defaults"

    classpath = sourceSets.main.runtimeClasspath
    main = "alice.tucson.service.TucsonNodeService"
}

task runCli(type: JavaExec, dependsOn:classes) {
    group = "run"
    description = "Starts a new Command Line Interface with defaults"

    standardInput = System.in
    classpath = sourceSets.main.runtimeClasspath
    main = "alice.tucson.service.tools.CommandLineInterpreter"
}

task runInspector (type: JavaExec, dependsOn:classes) {
    group = "run"
    description = "Starts a new InspectorGUI"

    classpath = sourceSets.main.runtimeClasspath
    main = "alice.tucson.introspection.tools.InspectorGUI"
}
